use std::str::FromStr;

use crate::ast::Program;
use crate::ast::FunctionDef;
use crate::ast::Expr;
use crate::tokens::*;

grammar;

pub Program: Program = {
    <functions:FunctionDef*> <main:ExprsList> => Program { functions, main }
};

ExprsList: Vec<Box<Expr>> = {
    <v:(<Expr> Semicolon)*> <last:Expr?> => {
        let mut vec = v;
        if let Some(e) = last {
            vec.push(e);
        }
        vec
    }
};

Expr: Box<Expr> = { EqualityExpr };

EqualityExpr: Box<Expr> = {
    <left:ComparisonExpr> <rest:(EqualityOp ComparisonExpr)*> => {
        rest.into_iter().fold(left, |l, (op, r)| {
            Box::new(Expr::BinaryOp(l, op, r))
        })
    }
};

Function: KeywordToken = {
    "function" => KeywordToken::FUNCTION,
};

Arrow: DelimiterToken = {
    "=>" => DelimiterToken::ARROW,
};

IdentifierList: Vec<String> = {
    <first:Identifier> <rest:(Comma Identifier)*> => {
        let mut vec = vec![first];
        for (_, id) in rest {
            vec.push(id);
        }
        vec
    }
};

FunctionDef: FunctionDef = {
    Function <name:Identifier> LParen <params:IdentifierList> RParen Arrow <body:Expr> Semicolon => {
        FunctionDef {
            name,
            params,
            body,
        }
    }
};

EqualityOp: OperatorToken = {
    "==" => OperatorToken::EQ,
    "!=" => OperatorToken::NEQ,
};

ComparisonExpr: Box<Expr> = {
    <left:TermExpr> <rest:(ComparisonOp TermExpr)*> => {
        rest.into_iter().fold(left, |l, (op, r)| {
            Box::new(Expr::BinaryOp(l, op, r))
        })
    }
};

ComparisonOp: OperatorToken = {
    ">" => OperatorToken::GT,
    ">=" => OperatorToken::GTE,
    "<" => OperatorToken::LT,
    "<=" => OperatorToken::LTE,
};

TermExpr: Box<Expr> = {
    <left:FactorExpr> <rest:(TermOp FactorExpr)*> => {
        rest.into_iter().fold(left, |l, (op, r)| {
            Box::new(Expr::BinaryOp(l, op, r))
        })
    }
};

TermOp: OperatorToken = {
    "+" => OperatorToken::PLUS,
    "-" => OperatorToken::MINUS,
};

FactorExpr: Box<Expr> = {
    <left:ExponentExpr> <rest:(FactorOp ExponentExpr)*> => {
        rest.into_iter().fold(left, |l, (op, r)| {
            Box::new(Expr::BinaryOp(l, op, r))
        })
    }
};

FactorOp: OperatorToken = {
    "*" => OperatorToken::MUL,
    "/" => OperatorToken::DIV,
    "%" => OperatorToken::MOD,
};

ExponentExpr: Box<Expr> = {
    <left:UnaryExpr> <op:PowOp> <right:ExponentExpr> => Box::new(Expr::BinaryOp(left, op, right)),
    UnaryExpr,
};

PowOp: OperatorToken = {
    "^" => OperatorToken::POW,
};

UnaryExpr: Box<Expr> = {
    <op:UnaryOp> <expr:UnaryExpr> => Box::new(Expr::UnaryOp(op, expr)),
    PrimaryExpr,
};

UnaryOp: OperatorToken = {
    "!" => OperatorToken::NOT,
    "-" => OperatorToken::NEG,
};

PrimaryExpr: Box<Expr> = {
    <name:Identifier> LParen <args:ArgList> RParen => Box::new(Expr::FunctionCall(name, args)),
    Num => Box::new(Expr::Number(<>)),
    Str => Box::new(Expr::Str(<>)),
    Identifier => Box::new(Expr::Identifier(<>)),
    LParen <Expr> RParen => Box::new(*<>),
    PrintExpr,
    True => Box::new(Expr::Boolean(true)),
    False => Box::new(Expr::Boolean(false)),
};

ArgList: Vec<Box<Expr>> = {
    // Handle non-empty arguments
    <first:Expr> <rest:(<Comma> <Expr>)*> => {
        let mut args = vec![first];
        for (_, expr) in rest {
            args.push(expr);
        }
        args
    },
    // Handle empty arguments (no parameters)
    => Vec::new()
};

PrintExpr: Box<Expr> = {
    Print LParen <Expr> RParen => Box::new(Expr::Print(<>)),
};

Comma: DelimiterToken = {
    "," => DelimiterToken::COMMA,
};

Semicolon: DelimiterToken = {
    ";" => DelimiterToken::SEMICOLON,
};

RParen: DelimiterToken = {
    ")" => DelimiterToken::RPAREN,
};

LParen: DelimiterToken = {
    "(" => DelimiterToken::LPAREN,
};

RBrace: DelimiterToken = {
    "}" => DelimiterToken::RBRACE,
};

LBrace: DelimiterToken = {
    "{" => DelimiterToken::LBRACE,
};

Let: KeywordToken = {
    "let" => KeywordToken::LET,
};

Else: KeywordToken = {
    "else" => KeywordToken::ELSE,
};

Elif: KeywordToken = {
    "elif" => KeywordToken::ELIF,
};

In: KeywordToken = {
    "in" => KeywordToken::IN,
};

If: KeywordToken = {
    "if" => KeywordToken::IF,
};

While: KeywordToken = {
    "while" => KeywordToken::WHILE,
};

Print: KeywordToken = {
    "print" => KeywordToken::PRINT,
};

True: KeywordToken = {
    "true" => KeywordToken::TRUE,
};

False: KeywordToken = {
    "false" => KeywordToken::FALSE,
};

Identifier: String = {
    r"[A-Za-z][A-Za-z_0-9]*" => String::from_str(<>).unwrap(),
};

Num: f64 = {
    r"[0-9]+(\.[0-9]+)?" => f64::from_str(<>).unwrap(),
};

Str: String = {
    r#""([^"\\]|\\.)*""# => String::from_str(&<>[1..<>.len()-1]).unwrap(),
};